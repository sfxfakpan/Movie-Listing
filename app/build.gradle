plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.francis.movielisting"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "API_KEY", getApiKey()
        buildConfigField "String", "API_URL", "\"https://api.themoviedb.org/\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

def getApiKey() {
    def file = rootProject.file("local.properties")
    def localProperties = new Properties()
    localProperties.load(new FileInputStream(file))
    def apiKey = localProperties['API_KEY']
    if (apiKey == null) {
        logger.error("You need to either place appropriate local.properties or set proper environment variables for API key")
    }
    return apiKey
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "androidx.navigation:navigation-fragment-ktx:2.5.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.3"

    //hilt for DI
    def hilt_version = '2.41'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    //Room
    def roomVersion = "2.4.1"
    implementation("androidx.room:room-runtime:$roomVersion") {
        exclude group: 'com.google.code.gson'
    }
    implementation("androidx.room:room-ktx:$roomVersion") {
        exclude group: 'com.google.code.gson'
    }
    kapt "androidx.room:room-compiler:$roomVersion"
    androidTestImplementation("androidx.room:room-testing:$roomVersion") {
        exclude group: 'com.google.code.gson'
    }

    implementation "io.coil-kt:coil:2.2.2"

    implementation project(':core')
}